// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
apply plugin: "com.moowork.node"

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task deployServer(type: Copy, dependsOn: ':server:jar') {
    from 'server/build/libs/server.jar'
    into 'deploy/'
}

task buildWebapp(type: NpmTask) {
    workingDir = file('webapp')
    args = ['run', 'build']
}

task deployWebapp(type: Copy, dependsOn: 'buildWebapp') {
    from 'webapp/build'
    into 'deploy/webapp/build'
}

task deployRunners(type: Copy) {
    from 'run_server.bat'
    into 'deploy'
}

task cleanDeploy(type: Delete){
    delete "deploy"
}

task deploy(type: Zip, dependsOn: ['cleanDeploy','deployServer', 'deployWebapp', 'deployRunners']) {
    from 'deploy'
    archiveName = "festerify_" + getDate() + ".zip"
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}